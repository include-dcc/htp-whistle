// 
def DocumentReferenceID(study, labid) {
    $this: $StrCat(study.id, ".", labid);
}

def KfDocAttachment(study, manifest) {
    url: manifest.drs_url;
    hash: manifest.hash_md5;
    size: manifest.size_bytes;
    title: manifest.filename;
}

def KfFileSizeExtension(study, manifest) {
    if (manifest.size_bytes?) {
        url: "https://nih-ncpi.github.io/ncpi-fhir-ig/StructureDefinition/file-size";
        valueDecimal: $ParseFloat(manifest.size_bytes);
    }
}

def KfFileHashExtension(study, hash, hash_type) {
    if (hash?) {
        url: "https://nih-ncpi.github.io/ncpi-fhir-ig/StructureDefinition/hashes";
        valueCodeableConcept: {
            coding[].display: hash_type;
            text: hash;
        }
    }
}

def KfFileDetailExtension(study, manifest) {
    extension[]: KfFileSizeExtension(study, manifest);

    if (manifest.etag_hash?) {
        extension[]: KfFileHashExtension(study, manifest.etag_hash, "etag");
    }
    if (manifest.md5_hash?) {
        extension[]: KfFileHashExtension(study, manifest.md5_hash, "md5");
    }
    if (manifest.sha1_hash?) {
        hash: manifest.sha1_hash;
    }
    url: manifest.drs_uri;
    title: manifest.filename; 
}

def KfDocReference(study, manifest, category) {
    var subject_id: manifest.participant_id;

    meta.tag[]: StudyMeta(study);
    meta.profile[]: "https://ncpi-fhir.github.io/ncpi-fhir-ig/StructureDefinition/ncpi-drs-document-reference";

    identifier[]: Key_Identifier(study, "DocumentReference", DocumentReferenceID(study, manifest.filename));
    identifier[0].use: "official";

    status: "current";
    docStatus: "final";
    type: manifest.data_category;
    subject: Reference_Key_Identifier(study, "Patient", subject_id);
    securityLabel: manifest.access_type;
    category[].text: manifest.experimental_strategy;
    category[].text: category;
    //content[]: KfDocAttachment(study, manifest);
    content[0].attachment.url: manifest.drs_uri;
    if (manifest.sha1_hash?) {
        content[0].attachment.hash: manifest.sha1_hash;
    }
    content[].attachment: KfFileDetailExtension(study, manifest);

    format.display: manifest.file_format;

    resourceType: "DocumentReference";
}

def FileSpecimenReference(study, manifest) {
    $this: Reference_Key_Identifier(study, "Specimen", SpecimenIdentifier(study, manifest.subject_id, manifest.sample_id));
}

def FileSpecimenAsInput(study, manifest) {
    type.text: "Specimen";
    valueReference: Reference_Key_Identifier(study, "Specimen", SpecimenIdentifier(study, manifest.subject_id, manifest.sample_id));
}

def KfFileTask(study, fileid, all_manifests) {

    var manifests: all_manifests[where $.file_id=fileid];

    meta.tag[]: StudyMeta(study);    
    meta.profile[]: "https://ncpi-fhir.github.io/ncpi-fhir-ig/StructureDefinition/ncpi-specimen-task";
    identifier[]: Key_Identifier(study, "Task", $StrCat(study.id, ".", fileid));
    identifier[0].use: "official";

    intent: "order";
    status: "completed";

    input[]: FileSpecimen(study, manifests[]);

    output[0].type.text: "DocumentReference";
    output[0].valueReference: Reference_Key_Identifier(study, "DocumentReference", DocumentReferenceID(study, manifests[0].subject_id, manifests[0].file_id));

    // This permits one to search for Tasks where the focus is a given specimen instance
    focus (if $ListLen(manifests) = 1): Reference_Key_Identifier(study, "Specimen", SpecimenIdentifier(study, manifests[0].subject_id, manifests[0].sample_id));

    // This is the patient, allowing one to query for all tasks associated with a given patient
    for (if $ListLen(manifests) = 1): Reference_Key_Identifier(study, "Patient", manifests[0].subject_id);

    resourceType: "Task";
}

def Reference_Doc_Enrollment(study, participantid) {
    if ($IsNotNil(participantid)) {
        entity: Reference_Key_Identifier(study, "Patient", participantid);
    }
}

def SubjectGroup(study, subjectids, filename) {
    meta.tag[]: StudyMeta(study);
    
    identifier[0]: Key_Identifier(study, "Group", filename);
    identifier[0].use: "official";

    type: "person";
    actual: true;

    member: Reference_Enrollment(study, subjectids[]);
    quantity: $ListLen(subjectids[*]);
    resourceType: "Group";
}

def KfFileCategoryMap(study, varname, value) {
    var code: HarmonizeMapped(value, varname);

    if (code?) {
        coding: code;
        text: value;
    }
}

def KfAcl(study, acl) {
    var pieces: $StrSplit(acl, ".");

    if ($ListLen(pieces) = 1) {
        text: acl
    } else {
        coding[0].code: pieces[1];
        text: acl;
    }
}

def KfDocumentReference(study, fileid, all_manifests) {
    var manifests: all_manifests[where $.file_id=fileid];
    var subject_ids: $Unique(manifests[*].subject_id);
    var specimen_ids: $Unique(manifests[*].htp_id);

    meta.tag[]: StudyMeta(study);
    meta.profile[]: "https://ncpi-fhir.github.io/ncpi-fhir-ig/StructureDefinition/ncpi-drs-document-reference";

    identifier[]: Key_Identifier(study, "DocumentReference", DocumentReferenceID(study, manifests[0].filename));
    identifier[0].use: "official";

    status: "current";
    docStatus: "final";
    type: KfFileCategoryMap(study, "data_type", manifests[0].data_type);

    if ($ListLen(subject_ids) = 1) {
        subject: Reference_Key_Identifier(study, "Patient", subject_ids[0]);
    }
    securityLabel[]: HarmonizeAsCoding(manifests[0].access_type, "AccessType");

    if (manifests[0].acl_list?) {
        var acls: $StrSplit(manifests[0].acl_list, ";");
        securityLabel[]: KfAcl(study, acls[]);
    }
    category[]: KfFileCategoryMap(study, "experiment_strategy", manifests[0].experiment_strategy);
    category[]: KfFileCategoryMap(study, "data_category", manifests[0].data_category);

    //content[]: KfDocAttachment(study, manifests);

    content[0].attachment: KfFileDetailExtension(study, manifests[0]);
    content[0].format.display: manifests[0].file_format

    // Makeshift mechanism to link Index files to their parent
    if (manifests[0].source_file?) {
        relatesTo[0].code: "transforms";
        relatesTo[0].target: Reference_Key_Identifier(study, "DocumentReference",  DocumentReferenceID(study, manifests[0].source_file));
    } else {
        context.related[]: FileSpecimenReference(study, manifests[]);
    }


    resourceType: "DocumentReference";
}

def ProcessKfManifestData(study, files) {
    var fileids: $Unique(files[*].file_id);

    //out files: KfFileTask(study, fileids[], files[*]);
    out files: KfDocumentReference(study, fileids[], files[*]);
    //out files: KfDocReference(study, files[], category);
}
