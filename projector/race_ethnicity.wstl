// Build Race and Ethnicity Extension representations
//
// Race and Ethnicity extensions will probably be useful for all sorts of 
// projects. The translation from local to FHIR's required coding must be
// handed by the harmonization component (i.e. the ConceptMap, probably
// constructed by Whistler)
//
// While the extensions themselves will be common for many if not all 
// studies, the format of the incoming data may vary. For this dataset,
// the data comes in as seperate "coumns" represented in JSON as individual
// objects with a value and code property. This code is a local code representing
// whatever coding is found in the original source data (in the case of ds-connect,
// that would be words and phrases like 'White' or 'Not Hispanic or ...')

// Definition: Add the Race extension if the data is present 
// 
// Argument:
//  race - This is a JSON object containing the response for one of the race checkboxes
//   code is the response, likely as provided inside the form
//   value is the checkbox value itself ("1" or "")
//
// We will ignore any race details that are not checked 
def RaceExtension(subject) {
    if (subject.race? and subject.race ~= "NA") {
        var RaceValueCoding: HarmonizeMappedFirst(subject.race, "race");

        if (RaceValueCoding?) {
            url: "http://hl7.org/fhir/us/core/StructureDefinition/us-core-race";
            extension[0].valueCoding : RaceValueCoding;
            extension[0].url : "ombCategory";
        }
    }
}
// Definition: Add the Race extension if the data is present 
// 
// Argument:
//  race - This is a JSON object containing the response for one of the race checkboxes
//   code is the response, likely as provided inside the form
//   value is the checkbox value itself ("1" or "")
//
// We will ignore any race details that are not checked 
// Add the ethnicity extension
def EthnicityExtension(subject) {
    if (subject.ethnicity? and subject.ethnicity ~= "NA") {
        var EthnicityValueCoding : HarmonizeMappedFirst(subject.ethnicity, "ethnicity");

        if (EthnicityValueCoding?) {
            if (EthnicityValueCoding.system ~= "ds-connect-harmony-unharmonized") {
                url : "http://hl7.org/fhir/us/core/StructureDefinition/us-core-ethnicity";
                extension[0].valueCoding : EthnicityValueCoding;
                extension[0].url : "ombCategory";

                extension[1].valueString (if subject.ethnicity_display?): subject.ethnicity_display;
                extension[1].url (if subject.ethnicity_display?): "text";
            } else {
                if (subject.ethnicity_display?) {
                    url : "http://hl7.org/fhir/us/core/StructureDefinition/us-core-ethnicity";
                    extension[0].valueString: subject.ethnicity_display;
                    extension[0].url: "text";
                }
            }    
        } 
    } 
}