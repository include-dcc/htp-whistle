// General utility whistle functions

// Description: Uniform method to generate Identifier structures. 
// 
// Arguments: 
//  study - This is the full study object created by Whistler
//  resourceType - The string value representing the FHIR's 
//                 resource type such as Observation or Condition
//  value - Most likely an id such as a subject_id 
//
// Please note that identifier-prefix is a Whistler attribute
// that is defined as part of the YAML configuration associated
// with the project/dataset itself. This can be defined however
// is appropriate. 
def Key_Identifier(study, required resourceType, required value) {
    value: value;
    system: $StrCat(study.identifier-prefix, "/", $ToLower(resourceType));
}

// Description: Returns the identifier object as an "identifier". 
//              This is suitable for using as references within 
//              transaction bundles. 
// 
// Arguments: 
//  study - The Whistler study object
//  resourceType - The string value representing the FHIR's 
//                 resource type such as Observation or Condition
//  value - Most likely an id such as a subject_id 

def Reference_Key_Identifier(study, required resourceType, required value) {
    identifier: Key_Identifier(study, resourceType, value);
}

// Description: Build a conventional reference if an ID is known
// 
// Arguments:
//  resourceType - The string value representing the FHIR's 
//                 resource type such as Observation or Condition
//  id - An actual ID associated with a FHIR resource.

def Build_Reference(required resourceType, required id) {
    reference : $StrCat(resourceType, "/",id);
}


// Description: Constructs a valid NCPI Vocabulary URL 
//
// Arguments:
//  study_id - unique ID associated with that study
//  table_name - The table name associated with this vocabulary
//  varname - The variable if this vocabulary describes the values associated 
//            with an actual variable's contents as opposed to members in a 
//            table
//   
// Please note that currently, varname can be an empty string, but it MUST 
// be present. 
def VocabularyURL(required study_id, required table_name, varname) {
  if ($IsNil(varname)) {
    $this: $StrCat("https://nih-ncpi.github.io/ncpi-fhir-ig/data-dictionary/", study_id, "/", table_name);
  } else {
    $this: $StrCat("https://nih-ncpi.github.io/ncpi-fhir-ig/data-dictionary/", study_id, "/", table_name, "/", varname); 
  }
}
