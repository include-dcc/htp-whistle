// Participant

// We create both a Patient record as well as a basic ResearchSubject here for each
// subject specified

// I'm not seeing any specimen details in the data I have at the moment

// Description: Research Subject 
//
// Arguments:
//  study - This is the full study object created by Whistler
//  subject - Must have a participantid
def ResearchSubject(study, subject) {
    meta.tag[]: StudyMeta(study);
    identifier[]: Key_Identifier(study, "ResearchSubject", subject.participantid);
    identifier[0].use: "official";
    status: "on-study";
    study: Reference_Key_Identifier(study, "ResearchStudy", study.id);
    individual: Reference_Key_Identifier(study, "Patient", subject.participantid);
    resourceType: "ResearchSubject";
}

// Description: Our Participant is basically a limited Patient record
//
// Arguments:
//  study - This is the full study object created by Whistler
//  subject - Must have a participantid
// 
def Participant(study, subject) {
    meta.tag[]: StudyMeta(study);
    identifier[]: Key_Identifier(study, "Patient", subject.participantid);
    identifier[0].use: "official";
    gender (if subject.sex ~= "."): HarmonizeAsCode(subject.sex, "gender");
    extension[]: RaceExtension(subject);
    extension[]: EthnicityExtension(subject);
    resourceType : "Patient"
}



// Description: Wrapper for generating Patient records
//
// Arguments:
//  study - This is the full study object created by Whistler
//  demo - Must have a participantid
// 
def ProcessParticipant(study, demo) {
    out patient: VariableObservation(study, demo.participantid, "participant", "MRAbstractionStatus", demo.mrabstractionstatus);
    out patient: AgeAtEnrollmentObservation(study, demo);
    out patient: Participant(study, demo);

    out observation: TraitObservation(study, demo.participantid, "Sex", demo.sex, "participant");
    out observation: TraitObservation(study, demo.participantid, "Race", demo.race, "participant");
    out observation: TraitObservation(study, demo.participantid, "Ethnicity", demo.ethnicity, "participant");
}

// Description: Wrapper for generating ResearchSubject records
//
// Arguments:
//  study - This is the full study object created by Whistler
//  demo - Must have a participantid
// 
def ProcessResearchSubject(study, demo) {
    out research_study: ResearchSubject(study, demo);
}
